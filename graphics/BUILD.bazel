load(
    "@ignition//tools/workspace:build_defs.bzl",
    "IGNITION_FEATURES",
    "IGNITION_ROOT",
    "IGNITION_VISIBILITY",
    "ign_configure_header",
    "ign_export_header",
    "ign_include_header",
)
load(
    "@ignition//tools/lint:lint.bzl",
    "add_lint_tests",
)

public_headers_no_gen = glob([
    "include/ignition/common/*.hh",
    "include/ignition/common/graphics/*.hh",
])

sources = glob(
    include = [
        "src/*.cc",
        "src/*.h",
    ],
    exclude = ["src/*_TEST.cc"],
)

test_sources = glob(["src/*_TEST.cc"])

ign_export_header(
    name = "include/ignition/common/graphics/Export.hh",
    export_base = "IGNITION_COMMON_GRAPHICS",
    lib_name = "ignition-common-graphics",
    visibility = ["//visibility:private"],
)

ign_include_header(
    name = "graphicshh_genrule",
    out = "include/ignition/common/graphics.hh",
    hdrs = public_headers_no_gen + [
        "include/ignition/common/graphics/Export.hh",
    ],
)

public_headers = public_headers_no_gen + [
    "include/ignition/common/graphics/Export.hh",
    "include/ignition/common/graphics.hh",
]

cc_library(
    name = "graphics",
    srcs = sources,
    hdrs = public_headers,
    includes = ["include"],
    visibility = ["//visibility:public"],
    deps = [
        IGNITION_ROOT + "ign_common",
        "@freeimage",
        "@glib",
        "@gts",
        "@tinyxml2",
    ],
)

[cc_test(
    name = src.replace("/", "_").replace(".cc", "").replace("src_", ""),
    srcs = [src],
    env = {
        "IGN_BAZEL": "1",
        "IGN_BAZEL_PATH": "ign_common",
    },
    deps = [
        ":graphics",
        IGNITION_ROOT + "ign_common:test_data",
        IGNITION_ROOT + "ign_common/testing",
        "@gtest",
        "@gtest//:gtest_main",
    ],
) for src in test_sources]

add_lint_tests()
